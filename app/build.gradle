plugins {
    id 'com.android.application'
    id 'com.github.triplet.play' version '4.0.0'
    id 'jacoco'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'com.github.bumptech.glide:glide:5.0.5'
    implementation 'com.github.gojuno.koptional:koptional:1.7.0'
    implementation 'com.github.rahatarmanahmed:circularprogressview:2.5.0'
    implementation 'com.github.takuji31.Koreference:koreference:2.0.0'
    implementation 'com.github.unhappychoice:circleci:1.0.0'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:2.2.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-design-kotlin:2.2.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.2.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:2.2.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:2.2.0'
    implementation 'com.pusher:pusher-java-client:2.4.4'
    implementation 'com.squareup.flow:flow:1.0.0-alpha3'
    implementation 'com.squareup.mortar:mortar:0.20'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:2.3.0'
    implementation 'org.kodein.di:kodein-di:7.30.0'
    testImplementation('com.github.raniejade.kspec:kspec-core:v0.4.0') { exclude group: 'org.jetbrains.kotlin' }
    testImplementation('com.github.raniejade.kspec:kspec-junit-runner:v0.4.0') { exclude group: 'org.jetbrains.kotlin' }
    testImplementation('com.winterbe:expekt:0.5.0') { exclude group: 'org.jetbrains.kotlin' }
    testImplementation 'io.mockk:mockk:1.13.16'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.16.1'
}

android {
    namespace = "com.unhappychoice.norimaki"
    compileSdk 36
    buildToolsVersion = '36.0.0'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlin {
        jvmToolchain(21)
    }
    signingConfigs {
        release {
            if (System.getenv("RELEASE_KEYSTORE_BASE64") != null) {
                storeFile decodeFileFromBase64Env('RELEASE_KEYSTORE_BASE64', "tmp_ks_", ".jks")
                storePassword System.getenv('RELEASE_KEYSTORE_PASSWORD')
                keyAlias System.getenv('RELEASE_KEYSTORE_KEY_ALIAS')
                keyPassword System.getenv('RELEASE_KEYSTORE_KEY_PASSWORD')
            }
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            debuggable false
            testCoverageEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (System.getenv("RELEASE_KEYSTORE_BASE64") != null) {
                signingConfig signingConfigs.release
            }
        }
    }
    buildFeatures {
        viewBinding true
    }
    defaultConfig {
        applicationId "com.unhappychoice.norimaki"
        minSdk 21
        targetSdk 35
        versionCode System.getenv("CIRCLE_BUILD_NUM") as Integer ?: 1
        versionName "1.1.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lint {
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    packaging {
        resources {
            excludes += [
                'META-INF/DEPENDENCIES.txt',
                'META-INF/LICENSE.txt',
                'META-INF/NOTICE.txt',
                'META-INF/NOTICE',
                'META-INF/LICENSE',
                'META-INF/DEPENDENCIES',
                'META-INF/notice.txt',
                'META-INF/license.txt',
                'META-INF/dependencies.txt',
                'META-INF/LGPL2.1',
                'META-INF/rxjava.properties',
                'META-INF/rxkotlin.properties'
            ]
        }
    }
    testOptions {
        unitTests {
            all {
                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
    }
}

play {
    enabled = System.getenv("RELEASE_KEYSTORE_BASE64") != null
    serviceAccountCredentials.set(decodeFileFromBase64Env("PLAY_ACCOUNT_JSON_BASE64", "norimaki", ".json"))
}

jacoco {
    toolVersion = "0.8.7"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacoco(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.required = true
        html.required = true
        classDirectories.setFrom(
                files(
                        fileTree(
                                dir: './build/intermediates/javac/debug/compileDebugJavaWithJavac/classes',
                                excludes: ['**/R.class', '**/R$*.class', '**/BuildConfig.class', '**/Manifest*.*', '**/*$inlined$*.*']
                        ) + fileTree(
                                dir: './build/tmp/kotlin-classes/debug',
                                excludes: ['**/R.class', '**/R$*.class', '**/BuildConfig.class', '**/Manifest*.*', '**/*$inlined$*.*']
                        )
                )
        )
    }
    sourceDirectories.from files('src/main/kotlin')
    executionData.from files('build/outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec')
}

static decodeFileFromBase64Env(String name, String fileName, String ext) {
    String base64String = System.getenv(name)
    if (base64String == null) return null
    File tempFile = File.createTempFile(fileName, ext, File.createTempDir())
    FileOutputStream fos = null
    try {
        fos = new FileOutputStream(tempFile)
        fos.write(base64String.decodeBase64())
        fos.flush()
    } finally {
        if (fos != null) {
            fos.close()
        }
    }
    return tempFile
}
